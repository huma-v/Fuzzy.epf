
Перем мВКПоиска;
Перем мМассивСущностей;


#Область ПрограмныйИнтерфейс

Процедура ИнициализоватьИндекс(ТаблицаИндекса = Неопределено, Симметричный = Ложь) Экспорт
	Если ТаблицаИндекса = Неопределено Тогда
		ТаблицаИндекса = Новый ТаблицаЗначений;
		ТаблицаИндекса.Колонки.Добавить();
		ТаблицаИндекса.Колонки.Добавить();
	КонецЕсли;
	
	лЗаписьJSON = Новый ЗаписьJSON;
	лЗаписьJSON.УстановитьСтроку();
	
	лЗаписьJSON.ЗаписатьНачалоМассива();
	
	Для лИдКолонки = 1 По ТаблицаИндекса.Колонки.Количество() - 1 Цикл
		ЗаписатьJSON(лЗаписьJSON, ТаблицаИндекса.ВыгрузитьКолонку(лИдКолонки));
	КонецЦикла;
	
	лЗаписьJSON.ЗаписатьКонецМассива();
	
	лСтрокаJSON = лЗаписьJSON.Закрыть();
	
	ВКПоиска().Reset(лСтрокаJSON, Симметричный);
	
	мМассивСущностей = ТаблицаИндекса.ВыгрузитьКолонку(0);
КонецПроцедуры


Функция ВыполнитьЗапрос(СтрокаЗапроса, ТопN = 0) Экспорт
	лРезультат = Новый ТаблицаЗначений;
	лРезультат.Колонки.Добавить("Объект");
	лРезультат.Колонки.Добавить("ИндексПоля", Новый ОписаниеТипов("Число"));
	лРезультат.Колонки.Добавить("КоэффициентСхожести", Новый ОписаниеТипов("Число"));
	
	лСтрокаJSON = ВКПоиска().Query(СтрокаЗапроса, ТопN);
	
	лЧтениеJSON = Новый ЧтениеJSON;
	лЧтениеJSON.УстановитьСтроку(лСтрокаJSON);
	
	лОтветКомпоненты = ПрочитатьJSON(лЧтениеJSON);
	
	Для Каждого лТекЭлемент Из лОтветКомпоненты Цикл
		лНов = лРезультат.Добавить();
		лНов.Объект = мМассивСущностей[лТекЭлемент[0]];
		лНов.ИндексПоля = лТекЭлемент[1];
		лНов.КоэффициентСхожести = лТекЭлемент[2];
	КонецЦикла;
	
	Возврат лРезультат;
КонецФункции

#КонецОбласти


Функция ВКПоиска()	
	Если мВКПоиска = Неопределено Тогда
		Если ПодключитьВнешнююКомпоненту(Метаданные().Макеты.FuzzyComp.ПолноеИмя(), "HV_FuzzyComp") Тогда
			мВКПоиска = New("AddIn.HV_FuzzyComp.FuzzyComp");
		Иначе
			ВызватьИсключение НСтр("ru = 'Не удалось подключить внешнюю компоненту ускорения поиска!'; uk = 'Не вдалося підключити зовнішню компоненту пришводшення пошуку!'");
		КонецЕсли;
	КонецЕсли;
	
	Возврат мВКПоиска;
КонецФункции
